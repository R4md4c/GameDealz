apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs"

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion project.ext.compileSdk
    defaultConfig {
        applicationId "de.r4md4c.gamedealz"
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        versionCode 4
        versionName "1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        buildConfigField("Integer", "DEFAULT_PAGE_SIZE", "20")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard-rules').asList().toArray()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')


    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.appcompat:appcompat:${versions.androidX}"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation "androidx.lifecycle:lifecycle-viewmodel:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidX}"
    implementation "androidx.paging:paging-runtime-ktx:${versions.paging_version}"
    implementation "androidx.annotation:annotation:1.0.1"
    implementation 'androidx.core:core-ktx:1.0.1'

    implementation "android.arch.navigation:navigation-fragment-ktx:${versions.nav_version}"
    implementation "android.arch.navigation:navigation-ui-ktx:${versions.nav_version}"
    implementation "com.google.android.material:material:${versions.material}"

    implementation 'com.github.stfalcon:stfalcon-imageviewer:0.1.0'

    implementation("com.github.bumptech.glide:glide:${versions.glide}") {
        exclude group: "com.android.support"
    }
    implementation("com.github.bumptech.glide:okhttp3-integration:${versions.glide}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation("com.squareup.okhttp3:okhttp:${versions.okhttp}") {
        because 'Glide okhttp integration needs this.'
    }
    implementation "com.squareup.okio:okio:${versions.okio}"
    kapt "com.github.bumptech.glide:compiler:${versions.glide}"

    implementation "ch.acra:acra-core:${versions.acra}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"

    implementation 'com.mikepenz:materialdrawer:6.1.1'
    implementation 'com.github.tinder:statemachine:0.1.2'
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    implementation "org.koin:koin-core:${versions.koin}"
    implementation "org.koin:koin-core-ext:${versions.koin}"
    implementation "org.koin:koin-android:${versions.koin}"
    implementation "org.koin:koin-androidx-scope:${versions.koin}"
    implementation "org.koin:koin-androidx-viewmodel:${versions.koin}"

    implementation project(':common')
    implementation project(':commonproviders')
    implementation project(':domain')

    testImplementation project(':commontest')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
    testImplementation "androidx.arch.core:core-testing:2.0.0"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.koin:koin-test:${versions.koin}"
    testImplementation "androidx.paging:paging-common-ktx:${versions.paging_version}"

    androidTestImplementation "androidx.test:rules:1.1.1"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
