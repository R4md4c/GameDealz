import io.gitlab.arturbosch.detekt.Detekt

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.6.21'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath BuildPlugins.androidGradlePlugin
        classpath BuildPlugins.kotlinGradlePlugin
        classpath BuildPlugins.navigationSafeArgsPlugin
        classpath BuildPlugins.kotlinAllOpenPlugin
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.39.0"
    id "io.gitlab.arturbosch.detekt" version "1.20.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }
}

subprojects { project ->
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        source = files("src/main/java")
        autoCorrect = true
        config = files("${rootProject.projectDir}/config/detekt/detekt.yml")
    }

    dependencies {
        detektPlugins BuildPlugins.detektFormatting
    }
}

tasks.withType(Detekt).configureEach {
    reports {
        html {
            enabled = true
            destination = file("$projectDir/build/reports/detekt.html")
        }
        xml {
            enabled = false
        }
    }
    autoCorrect = true
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: GameDealzPlugin
