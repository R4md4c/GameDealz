apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-parcelize'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion AndroidSdk.compile
    defaultConfig {
        applicationId "de.r4md4c.gamedealz"
        minSdkVersion AndroidSdk.min
        targetSdkVersion AndroidSdk.target
        versionCode providers.environmentVariable("CIRCLE_BUILD_NUM").getOrElse("14").toInteger()
        versionName "1.12"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    signingConfigs {
        gamedealz {
            keyAlias = providers.environmentVariable("KEY_STORE_ALIAS").orNull
            keyPassword = providers.environmentVariable("KEY_STORE_PASSWORD").orNull
            storeFile = file("$rootDir/gamedealz.keystore")
            storePassword = providers.environmentVariable("KEY_STORE_FILE_PASSWORD").orNull
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard-rules').asList().toArray()
            signingConfig = signingConfigs.gamedealz
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation Libraries.multidex
    implementation Libraries.appCompat
    implementation Libraries.constraintLayout
    implementation Libraries.lifecycleViewModel
    implementation Libraries.lifecycleViewModelKtx
    implementation Libraries.lifecycleRuntimeKtx
    implementation Libraries.recyclerView
    implementation Libraries.pagingRuntimeKtx
    implementation Libraries.androidXAnnotations
    implementation Libraries.coreKtx
    implementation Libraries.roomRuntime

    implementation Libraries.navigationFragmentKtx
    implementation Libraries.navigationUiKtx
    implementation Libraries.material

    implementation Libraries.swipeRefreshLayout

    implementation(Libraries.glide) {
        exclude group: "com.android.support"
    }
    implementation(Libraries.glideHttpIntegration) {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation(Libraries.okhttp) {
        because 'Glide okhttp integration needs this.'
    }
    implementation (Libraries.workManagerKtx) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-android'
    }
    implementation Libraries.okio
    kapt Libraries.glideCompiler

    implementation Libraries.acraCore

    implementation Libraries.kotlinStdLib
    implementation Libraries.kotlinCoroutines

    implementation Libraries.fastAdapter
    implementation Libraries.materialDrawer
    implementation Libraries.stateMachine
    implementation Libraries.timber
    implementation Libraries.timberSlf4j

    implementation project(':common-ui')
    implementation project(':features:deals')
    implementation project(':features:detail')
    implementation project(':features:search')
    implementation project(':features:watchlist')
    implementation project(':features:regions')
    implementation project(':features:home')
    implementation project(':core')
    implementation project(':workers')
    implementation project(':common')
    implementation project(':commonproviders')
    implementation project(':data')
    implementation project(':network')
    implementation project(':domain')
    implementation project(':auth')

    testImplementation project(':commontest')
    testImplementation TestLibraries.liveDataTesting
    testImplementation TestLibraries.liveDataTestingKtx
    testImplementation TestLibraries.coroutinesTest
    testImplementation TestLibraries.archComponentsCoreTesting
    testImplementation TestLibraries.kotlinReflect
    testImplementation TestLibraries.junit
    testImplementation TestLibraries.robolectric
    testImplementation TestLibraries.androidXTestCore
    testImplementation Libraries.pagingCommonKtx

    androidTestUtil TestLibraries.testOrchestrator
    androidTestImplementation TestLibraries.uiAutomator
    androidTestImplementation project(':commontest')
    androidTestImplementation TestLibraries.testRules
    androidTestImplementation TestLibraries.testRunner
    androidTestImplementation TestLibraries.espressoCore
    androidTestImplementation TestLibraries.androidXTestCore
}
