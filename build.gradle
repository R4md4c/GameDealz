// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath BuildPlugins.androidGradlePlugin
        classpath BuildPlugins.kotlinGradlePlugin
        classpath BuildPlugins.navigationSafeArgsPlugin
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.21.0"
    id "io.gitlab.arturbosch.detekt" version "1.1.1"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        input = files("src/main/java")
        autoCorrect = true
        reports {
            html {
                enabled = true
                destination = file("$projectDir/build/reports/detekt.html")
            }
            xml {
                enabled = false
            }
        }
        config = files("${rootProject.projectDir}/config/detekt/detekt.yml")
    }

    dependencies {
        detektPlugins BuildPlugins.detektFormatting
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    autoCorrect = true
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
